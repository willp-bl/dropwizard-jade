plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

repositories {
    if (System.getenv('VERIFY_USE_PUBLIC_BINARIES') == 'true') {
        mavenCentral()
    }
    else {
        maven { url 'https://artifactory.ida.digital.cabinet-office.gov.uk/artifactory/whitelisted-repos' }
    }
}

def dependencyVersions = [
        dropwizard:"1.1.4"
]

dependencies {
    compile "io.dropwizard:dropwizard-views:$dependencyVersions.dropwizard",
            'de.neuland-bfi:jade4j:0.4.0',
            'com.google.guava:guava:19.0',
            'org.apache.commons:commons-lang3:3.4',
            'org.glassfish.jersey.core:jersey-server:2.23.2'

    testCompile 'junit:junit:4.11',
            'org.assertj:assertj-core:1.6.0'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'uk.gov.ida'
            artifactId 'dropwizard-jade'
            version "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "file:/srv/maven" // change to point to your repo, e.g. http://my.org/repo
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven-test'
        name = 'dropwizard-jade'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/dropwizard-jade.git'
        version {
            name = "$dependencyVersions.dropwizard-${System.env.BUILD_NUMBER ?: 'SNAPSHOT'}"
        }
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

defaultTasks 'clean', 'publishToMavenLocal'
